@echo off
chcp 65001 >nul
set debug=echo //
set debug=rem 
echo ---8^<--------8^<--------8^<--------8^<--------8^<--------8^<-----
echo(function loadCacheContent^(cache^) {
echo(  //list generated by get_base_files script
echo(  return cache.addAll^(^[
call :addFolder
call :addFolder res
call :addFolder gedcomjs
call :addFolder fonts
REM for /F "delims=*" %%L in ('dir /a:-h/b/o:n %files%') do call :add "%%~L" ./
REM for /F "delims=*" %%L in ('dir /a:-h/b/o:n docs\fonts') do call :add "%%~L" ./%%~dpnxL/
echo(    "./favicon.ico"
echo(  ^]^);
echo(}
echo ---8^<--------8^<--------8^<--------8^<--------8^<--------8^<-----
echo.
pause
goto :eof
:addFolder
set folder=./%1
if "%folder%" == "./" set folder=.
%debug% folder=%folder%
set subdir=%folder:/=\%
%debug% //subdir=%subdir%
set files=
set files=%files% docs\%subdir%\*.html
set files=%files% docs\%subdir%\*.css
set files=%files% docs\%subdir%\*.js
set files=%files% docs\%subdir%\*.json
set files=%files% docs\%subdir%\*.png
set files=%files% docs\%subdir%\*.jpg
set files=%files% docs\%subdir%\*.ttf
set files=%files% docs\%subdir%\*.woff*
set files=%files% 
%debug% files=%files%
for /F "delims=*" %%L in ('dir /a:-h/b/o:n %files%') do call :add "%%~L" %folder%/
goto :eof
:add
if "%1" == "" goto :eof
%debug% :: %*
if "%~n1" == "viewworker" goto :eof
set "filename=%~1"
set "filename=%filename:\=/%"
echo(    "%~2%filename%",
